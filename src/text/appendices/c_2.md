### C.2: Clausal logic and resolution: theoretical backgrounds ###

```{exercise} 2.1
Translate the following statements into clauses, using the atoms `person`, `sad` and `happy`:

a. persons are happy or sad;
b. no person is both happy and sad;
c. sad persons are not happy;
d. non-happy persons are sad.
```

The statements should be read as &lsquo;**if** &hellip; **then** &hellip;&rsquo; statements. Thus, the first statement reads &lsquo;**if** somebody is a person, **then** she is happy or sad&rsquo;:
> (*a.*) `happy;sad:-person`

The second statement reads &lsquo;**if** somebody is a person, **then** she is not both happy and sad&rsquo;. In clausal logic, only positive conclusions can be drawn; negative conclusions are turned into positive conditions, as follows: &lsquo;**if** somebody is a person, and she is happy and sad, **then** contradiction&rsquo;. A contradictory conclusion is signalled by the empty head:
> (*b.*) `:-person,happy,sad`

Following the same recipe, the third statement expresses that &lsquo;**if** somebody is a person who is sad, and she is happy, **then** contradiction&rsquo;:
> (*c.*) `:-person,sad,happy`

Thus, sentences (*b.*) and (*c.*) convey the same logical meaning.

Finally, the fourth sentence reads &lsquo;**if** somebody is a person who is not happy, **then** she is sad&rsquo;. In clausal logic, only positive conditions can be used; therefore, this negative condition should be turned into a positive conclusion: &lsquo;**if** somebody is a person, **then** she is sad or happy&rsquo;. We thus obtain the same clause as in case (*a.*):
> (*d.*) `sad;happy:-person`

```{exercise} 2.2
Given the program<br>
  ```Prolog
  married;bachelor:-man,adult.
  man.
  :-bachelor.
  ```
determine which of the following clauses are logical consequences of this program:

a. `married:-adult`;
b. `married:-bachelor`;
c. `bachelor:-man`;
d. `bachelor:-bachelor`.
```

a. Any model of the first clause, which additionally makes `man` **true**, is also a model of the clause `married;bachelor:-adult`. Likewise, any model of this clause which additionally makes `bachelor` **false** is also a model of the clause `married:-adult`, which is therefore a logical consequence of the program.
b. The body of this clause is **false** in any model of the program, and therefore the clause is **true** in any such model.
c. The body of this clause is **true** in any model of the program, while its head is **false**. The clause is therefore not a logical consequence of the program (on the contrary, it is **false** in every model of the program, not just in some).
d. This clause is a *tautology*: it is **true** in any interpretation, and therefore a logical consequence of any program.

```{exercise} 2.3
Write down the six Herbrand interpretations that are not models of the program
  ```Prolog
  married;bachelor:-man,adult.
  has_wife:-man,married.
  ```
```

The six interpretations are:
```Prolog
{man,adult}
{man,adult,has_wife}
{man,married}
{man,married,adult}
{man,married,bachelor}
{man,married,adult,bachelor}
```
The first two interpretations satisfy the body of the first clause but violate its head; the remaining four interpretations satisfy the body of the second clause but violate its head.

```{exercise} 2.4
Give a derivation of `friendly` from the following program:
  ```Prolog
  happy;friendly:-teacher.
  friendly:-teacher,happy.
  teacher;wise.
  teacher:-wise.
  ```
```

This requires derivations of the clauses `friendly:-teacher` and `teacher`:
```{figure} ../images/part_a/image004.svg
---
width: 70%
name: a.2
---
```
Notice that this derivation can not be recast in the form of a linear tree, where each resolvent is obtained from the previous resolvent and a given clause, as in Chapter 1. This is due to the fact that some clauses are indefinite (have more than one positive literal).

```{exercise} 2.5
Prove by refutation that `friendly:-has_friends` is a logical consequence of the following clauses:
  ```Prolog
  happy:-has_friends.
  friendly:-happy.
  ```
```

The negation of `friendly:-has_friends` consists of two clauses, `:‑friendly` and `has_friends`. Together, these four clauses are inconsistent:
```{figure} ../images/part_a/image006.svg
---
width: 35%
name: a.3
---
```

```{exercise} 2.6
How many models does the following clause have over the Herbrand universe `{peter,maria}`:
  ```Prolog
  likes(peter,S):-student_of(S,peter)
  ```
```

The set of ground instances of this clause is
```Prolog
{likes(peter,maria):-student_of(maria,peter),
 likes(peter,peter):-student_of(peter,peter)}
```
and the Herbrand base is
```Prolog
{likes(peter,peter),likes(peter,maria),
 likes(maria,peter),likes(maria,maria),
 student_of(peter,peter),student_of(peter,maria),
 student_of(maria,peter),student_of(maria,maria)}
```
Only the left four ground atoms are relevant for determining whether an interpretation is a model. 9 out of 16 truth-value assignments to these ground atoms result in a model. Because of the 4 irrelevant ground atoms, this yields $9*2^4=144$ models. Notice that this is a rather large number of models for such a modest Herbrand universe, and such a simple clause! This illustrates that *less knowledge leads to more models*.

```{exercise} 2.7
Write a clause expressing that Peter teaches all the first-year courses, and apply   resolution to this clause and the clause
  ```Prolog
  likes(peter,maria):-follows(maria,C),teaches(peter,C)
  ```
```

This is expressed by the clause
```Prolog
teaches(peter,C):-first_year_course(C)
```
Resolution with the above clause yields
```Prolog
likes(peter,maria):-follows(maria,C),first_year_course(C)
```
In words: &lsquo;Peter likes Maria **if** Maria follows a first-year course&rsquo;.

```{exercise} 2.9
Translate to clausal logic:

a. every mouse has a tail;
b. somebody loves everybody;
c. every two numbers have a maximum.
```

a. This statement should be read as &lsquo;**if** *X* is a mouse, **then** there exists something which is *X*&rsquo;s tail&rsquo;. Giving *X*&rsquo;s tail the abstract name `tail(X)`, we obtain the following clause:
   ```Prolog
   tail_of(tail(X),X):-mouse(X)
   ```
b. Here we need to give the person who loves everybody an abstract name. Since this person does not depend on anybody else, it can simply be a constant:
   ```Prolog
   loves(person_who_loves_everybody,X)
   ```
   Notice the difference with the statement &lsquo;everybody loves somebody&rsquo;:
   ```Prolog
   loves(X,person_loved_by(X))
   ```
c. This statement should be read as &lsquo;**if** *X* and *Y* are numbers, then there exists a number which is their maximum&rsquo;. Giving this maximum the abstract name `max(X,Y)` yields the clause
   ```Prolog
   maximum(X,Y,max(X,Y)):-number(X),number(Y)
   ```

```{exercise} 2.10
Determine the Herbrand universe of the following program:
  ```Prolog
  length([],0).
  length([X|Y],s(L)):-length(Y,L).
  ```
(Hint: recall that `[]` is a constant, and that `[X|Y]` is an alternative notation for the complex term `.(X,Y)` with binary functor &lsquo;`.`&rsquo;!)
```

In the intended interpretation, `s` is restricted to numbers and &lsquo;`.`&rsquo; is restricted to lists; however, variables are untyped in clausal logic, and the two sets of terms may be mixed. Thus, the Herbrand universe will contain terms denoting numbers, such as
```Prolog
0,s(0),s(s(0)),s(s(s(0))),…
```
and terms denoting lists of numbers, such as
```Prolog
[],[0],[s(0),0],[s(s(0)),s(0),0],…
```
but also &lsquo;strange&rsquo; terms like
```Prolog
[[[0]]] or .(.(.(0,[]),[]),[])
[s(0)|0] or .(s(0),0)
[s([[]|0])]
```
and so on.

```{exercise} 2.11
If possible, unify the following pairs of terms:

a. `plus(X,Y,s(Y))` and `plus(s(V),W,s(s(V)))`;
b. `length([X|Y],s(0))` and `length([V],V)`;
c. `larger(s(s(X)),X)` and `larger(V,s(V))`.
```

a. `plus(s(V),s(V),s(s(V)))`.
b. `length([s(0)],s(0))`.
c. Not unifiable.

```{exercise} 2.13
Write a clause for the statement &lsquo;somebody is innocent unless proven guilty&rsquo;, and give its intended model (supposing that `john` is the only individual in the Herbrand universe).
```

The clause is
```Prolog
innocent(X):-not guilty(X)
```
with intended model `{innocent(john)}`.

```{exercise} 2.14
Translate to clausal logic:

a. $\forall \texttt{X} \exists \texttt{Y: mouse(X)} \rightarrow \texttt{tail_of(Y,X)}$;
b. $\forall \texttt{X} \exists \texttt{Y: loves(X,Y)} \land \texttt{(} \forall \texttt{Z: loves(Y,Z))}$;
c. $\forall \texttt{X} \forall \texttt{Y} \exists \texttt{Z: number(X)} \land \texttt{number(Y)} \rightarrow \texttt{maximum(X,Y,Z)}$.
```

a. This statement translates almost immediately into a clause, replacing the existential quantifier by a Skolem functor `tail`:
   ```Prolog
   tail_of(tail(X),X):-mouse(X)
   ```
b. This formula is already in conjunctive normal form, and each conjunct yields a separate clause. After replacing the existential quantifier by a Skolem functor `person_loved_by`, we obtain
   ```Prolog
   loves(X,person_loved_by(X)).
   loves(person_loved_by(X),Z).
   ```
   Notice that the two clauses are &lsquo;linked&rsquo; by the Skolem functor.
c. Here, the Skolem functor has two arguments:
   ```Prolog
   maximum(X,Y,max(X,Y)):-number(X),number(Y)
   ```
   See also Exercise 2.9.
